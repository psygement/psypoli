# BUGS/JAGS Code to generate DIF-corrected estimates of stimuli positions
# DIF-corrected respondent positions can be derived post-estimation using 
# the a and b parameters and the reported self-placements.
# z[i,j] is reported respondent i placement of stim j (self-placements have been removed)
# N is number of rows
# q is number of columns
# zhat[j] is 'true' position of stimulus j
# a[i] and b[i] are individual linear mapping params

model{
for(i in 1:N){   ##loop through respondents
        for(j in 1:q){ ##loop through stimuli
			z[i,j] ~ dnorm(mu[i,j], tau[i,j])
			mu[i,j] <- a[i] + b[i]*zhat[j]
			tau[i,j] <- tauj[j] * taui[i]
}	
}

##priors on a and b
for(i in 1:N){
a[i] ~ dunif(-100, 100)
b[i] ~ dunif(-100, 100)
}

##priors on variance
for(j in 1:q){
tauj[j] ~ dgamma(.1,.1)
}
for(i in 1:N){
taui[i] ~ dgamma(ga,gb)
}
ga ~ dgamma(.1,.1)
gb ~ dgamma(.1,.1)

##priors on zhat
zhat[1] ~ dnorm(0,1)T(-1.1,-0.9)
zhat[2] ~ dnorm(0,1)T(0.9,1.1)
for (j in 3:q){
zhat[j] ~ dnorm(0,1)
}

sigmai.sq <- 1/taui
sigmaj.sq <- 1/tauj

}

